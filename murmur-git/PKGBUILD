# Maintainer: Otto Allmendinger <otto.allmendinger@googlemail.com>


pkgname="murmur-git"
pkgver=20091218
pkgrel=1
pkgdesc="The voice chat application server for Mumble (git version)"
arch=("i686" "x86_64")
url="http://mumble.sourceforge.net"
license=('GPL')
depends=("avahi" "lsb-release" "protobuf" "qt>=4.4.0")
makedepends=("git" "boost")
conflicts=("murmur" "murmur-static" "murmur-ice")
backup=("etc/murmur.ini"
    "etc/logrotate.d/murmur"
    "etc/conf.d/murmur")
install="murmur.install"
source=("murmur.rc.d"
    "murmur.conf.d"
    "murmur.logrotate.d"
    "murmur.dbus.conf")

md5sums=('b99061be67c54bf0bb4a061fd1280281'
         '11c1cc875e8021739ec2087252cf0533'
         'c56726cb267ead68f37f35c28e36e6fa'
         'eddea4cdbd0bde4b960a67e16b5d5478')


_gitroot="git://mumble.git.sf.net/gitroot/mumble/mumble"
_gitname="mumble"


build() {
    cd $srcdir

    msg "Connecting to GIT Server..."

    if [ -d $_gitname ]; then
        cd $_gitname && git pull origin || return 1
        msg "The local files are updated"
    else
        git clone $_gitroot || return 1
    fi

    [ -e "$srcdir/$_gitname-build" ] && rm -rf "$srcdir/$_gitname-build"

    git clone "$srcdir/$_gitname" "$srcdir/$_gitname-build" || return 1

    cd "$srcdir/$_gitname-build"

    msg "GIT checkout done"

    # build

    msg "Starting make..."

    qmake main.pro CONFIG+="no-client no-ice"

    make || return 1

    sed -e "s|database=|database=/var/lib/murmur/murmur.sqlite|" \
        -e "s|dbus=session|#dbus=session|" \
        -e "s|#logfile=murmur.log|logfile=/var/log/murmur/murmur.log|" \
        -e "s|#pidfile=|pidfile=/var/run/murmur/murmur.pid|" \
        -e "s|#uname=|uname=murmur|" \
        -i scripts/murmur.ini || return 1

    # install

    install -dm755 ${pkgdir}/var/lib/murmur
    install -dm755 ${pkgdir}/var/log/murmur
    install -dm755 ${pkgdir}/var/run/murmur
    install -Dm755 release/murmurd ${pkgdir}/usr/sbin/murmurd
    install -Dm644 scripts/murmur.ini ${pkgdir}/etc/murmur.ini
    install -Dm755 ${startdir}/murmur.rc.d ${pkgdir}/etc/rc.d/murmur
    install -Dm644 ${startdir}/murmur.conf.d ${pkgdir}/etc/conf.d/murmur
    install -Dm644 ${startdir}/murmur.dbus.conf \
                    ${pkgdir}/etc/dbus-1/system.d/murmur.conf
    install -Dm644 ${startdir}/murmur.logrotate.d \
                    ${pkgdir}/etc/logrotate.d/murmur
    install -Dm644 README ${pkgdir}/usr/share/doc/murmur/README
    install -Dm644 man/murmurd.1 ${pkgdir}/usr/share/man/man1/murmurd.1
}
