# Maintainer: Otto Allmendinger <otto.allmendinger@gmail.com>
# https://github.com/OttoAllmendinger/aur-packages

_use_zeroc_ice="0"

pkgname="murmur-git"
pkgver=1.2.3.534.g1c085e4
pkgrel=1
pkgdesc="The voice chat application server for Mumble (git version)"
arch=("i686" "x86_64")
url="http://mumble.sourceforge.net"
license=('GPL')
depends=("avahi" "lsb-release" "protobuf" "qt4")

if [[ ${_use_zeroc_ice} == "1" ]]; then
    depends=( "${depends[@]}" zeroc-ice )
fi

makedepends=("git" "boost" "qt4")
conflicts=("murmur" "murmur-static" "murmur-ice")
backup=("etc/murmur.ini"
    "etc/logrotate.d/murmur"
    "etc/conf.d/murmur")
install="murmur.install"
source=(
    "git://mumble.git.sf.net/gitroot/mumble/mumble"
    "murmur.rc.d"
    "murmur.conf.d"
    "murmur.logrotate.d"
    "murmur.tmpfiles.d"
    "murmur.dbus.conf"
    "murmur.service"
    "0001-use-std-max-instead-of-MAX.patch")

_gitname="mumble"

pkgver() {
    cd ${srcdir}/$_gitname
    git describe | sed 's/^v//;s/-/./g'
}

build() {
    cd ${srcdir}/${_gitname}

    patch -p1 < "$srcdir/0001-use-std-max-instead-of-MAX.patch"

    # build

    if [[ $_use_zeroc_ice == "1" ]]; then
        qmake-qt4 main.pro CONFIG+="no-client"
    else
        qmake-qt4 main.pro CONFIG+="no-client no-ice"
    fi

    make release
}

package() {
    cd ${srcdir}/${_gitname}

    sed -e "s|database=|database=/var/lib/murmur/murmur.sqlite|" \
        -e "s|dbus=session|#dbus=session|" \
        -e "s|#logfile=murmur.log|logfile=/var/log/murmur/murmur.log|" \
        -e "s|#pidfile=|pidfile=/var/run/murmur/murmur.pid|" \
        -e "s|#uname=|uname=murmur|" \
        -i scripts/murmur.ini

    install -dm755 ${pkgdir}/var/lib/murmur
    install -dm755 ${pkgdir}/var/log/murmur
    install -dm755 ${pkgdir}/var/run/murmur
    install -Dm755 release/murmurd ${pkgdir}/usr/sbin/murmurd
    install -Dm644 scripts/murmur.ini ${pkgdir}/etc/murmur.ini
    install -Dm755 ${srcdir}/murmur.rc.d ${pkgdir}/etc/rc.d/murmur
    install -Dm644 ${srcdir}/murmur.conf.d ${pkgdir}/etc/conf.d/murmur
    install -Dm644 ${srcdir}/murmur.dbus.conf \
                    ${pkgdir}/etc/dbus-1/system.d/murmur.conf
    install -Dm644 ${srcdir}/murmur.logrotate.d \
                    ${pkgdir}/etc/logrotate.d/murmur
    install -Dm644 README ${pkgdir}/usr/share/doc/murmur/README
    install -Dm644 man/murmurd.1 ${pkgdir}/usr/share/man/man1/murmurd.1
    install -Dm644 ${srcdir}/murmur.tmpfiles.d \
                    ${pkgdir}/usr/lib/tmpfiles.d/murmur.conf
    install -Dm644 ${srcdir}/murmur.service \
                    ${pkgdir}/usr/lib/systemd/system/murmur.service

    if [[ $_use_zeroc_ice == "1" ]]; then
        install -Dm644 src/murmur/Murmur.ice \
            ${pkgdir}/usr/share/murmur/Murmur.ice
    fi
}

md5sums=('SKIP'
         'b99061be67c54bf0bb4a061fd1280281'
         '11c1cc875e8021739ec2087252cf0533'
         'c56726cb267ead68f37f35c28e36e6fa'
         'e00d0d2610b77efebce5b3556609251f'
         'eddea4cdbd0bde4b960a67e16b5d5478'
         '5ad8798aea4f8b70f47f16df64f755b3'
         '0977b21b5f1567972e59800beb34d3cd')
