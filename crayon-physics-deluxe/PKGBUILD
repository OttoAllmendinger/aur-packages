# Maintainer: Gadget3000 <gadget3000 at msn dot com>
# Contributor: Otto Allmendinger <otto.allmendinger@gmail.com>

pkgname=crayonphysicsdeluxe
pkgver=55_2
pkgrel=3
pkgdesc="A 2D physics puzzle / sandbox game, in which your drawings are transformed into objects"
arch=('i686' 'x86_64')
url="http://www.crayonphysics.com/"
license=('custom')
if [ "$CARCH" = "x86_64" ]; then
	depends=('lib32-mesa' 'lib32-libpulse' 'lib32-libjpeg6' 'lib32-libtiff4' 'lib32-libmikmod' 'lib32-smpeg' 'lib32-libidn' 'lib32-openssl-compatibility' 'lib32-nas' 'lib32-libpng12')
else
        depends=('mesa' 'libpulse' 'libjpeg6' 'libtiff4' 'libmikmod' 'smpeg' 'libidn' 'openssl-compatibility' 'nas' 'libpng12')
fi
makedepends=("imagemagick")
install=("CrayonPhysicsDeluxe.install")
source=("CrayonPhysicsDeluxe.desktop" "crayon.sh")
md5sums=('77b35434d5d3d52f7f6071caf306f1d1'
         '600607a7c5dfeed25a894b9c31125859')

_archive="crayon_release55_2.tar.gz"
_archive_md5="280c0a6a657b5de6729d8d4f6add4978"

build() {
  cd $srcdir

  if [ ! -f ${_archive} ]; then
    echo "Please place ${_archive} into $(pwd)"
    return 1
  fi

  if ! echo "${_archive_md5}  ${_archive}" | md5sum -c --quiet; then
    echo "Invalid checksum for ${_archive}"
    return 1
  fi

  install -d ${pkgdir}/opt/
  tar xzf ${_archive} -C ${pkgdir}/opt/
  chmod +x ${pkgdir}/opt/CrayonPhysicsDeluxe/launcher
  install -d ${pkgdir}/usr/bin

  for i in 16x16 22x22 32x32 48x48 64x64 128x128; do
    mkdir -p ${pkgdir}/usr/share/icons/hicolor/${i}/apps
    convert -resize ${i} \
	${pkgdir}/opt/CrayonPhysicsDeluxe/data/gfx/big_star.png \
	${pkgdir}/usr/share/icons/hicolor/${i}/apps/CrayonPhysicsDeluxe.png
  done

  install -Dm644 ${startdir}/CrayonPhysicsDeluxe.desktop \
		 ${pkgdir}/usr/share/applications/CrayonPhysicsDeluxe.desktop

  install -d ${pkgdir}/usr/share/licenses/CrayonPhysicsDeluxe/
  ln -s /opt/CrayonPhysicsDeluxe/LICENSE.txt \
	${pkgdir}/usr/share/licenses/CrayonPhysicsDeluxe/

  install -Dm755 ${startdir}/crayon.sh	${pkgdir}/usr/bin/crayon
}
