# Maintainer: Otto Allmendinger <otto.allmendinger@gmail.com>
# https://github.com/OttoAllmendinger/aur-packages

pkgname="murmur-static"
pkgver=1.2.3
_pkgresourcename="murmur-static_x86"
pkgrel=4
pkgdesc="The voice chat application server for Mumble (static version)"
arch=("i686" "x86_64")
url="http://mumble.sourceforge.net"
license=("GPL")
makedepends=("tar")
optdepends=("zeroc-ice: remote scripting support")
conflicts=("murmur" "murmur-git" "murmur-ice")
backup=("etc/murmur.ini"
        "etc/logrotate.d/murmur"
        "etc/conf.d/murmur")
install="murmur.install"
source=("http://downloads.sourceforge.net/sourceforge/mumble/$_pkgresourcename-$pkgver.tar.bz2" 
    "murmur.rc.d"
    "murmur.logrotate.d"
    "murmur.tmpfiles.d"
    "murmur.service"
    "murmur.conf.d"
    "murmurd.1.man")


build() {
    bsdtar -xf $_pkgresourcename-$pkgver.tar.bz2
}

package() {
    cd ${srcdir}/$_pkgresourcename-$pkgver/

    sed -e "s|database=|database=/var/lib/murmur/murmur.sqlite|" \
        -e "s|dbus=session|#dbus=session|" \
        -e "s|#logfile=murmur.log|logfile=/var/log/murmur/murmur.log|" \
        -e "s|#pidfile=|pidfile=/var/run/murmur/murmur.pid|" \
        -e "s|#uname=|uname=murmur|" \
        -i murmur.ini

    install -dm755 $pkgdir/var/lib/murmur
    install -dm755 $pkgdir/var/log/murmur
    install -dm755 $pkgdir/var/run/murmur
    install -Dm755 murmur.x86 $pkgdir/usr/sbin/murmurd
    install -Dm644 murmur.ini $pkgdir/etc/murmur.ini
    install -Dm644 README $pkgdir/usr/share/doc/murmur/README
    install -Dm644 ${srcdir}/murmur.logrotate.d $pkgdir/etc/logrotate.d/murmur
    install -Dm755 ${srcdir}/murmur.rc.d $pkgdir/etc/rc.d/murmur
    install -Dm644 ${srcdir}/murmur.conf.d $pkgdir/etc/conf.d/murmur
    install -Dm644 ${srcdir}/murmurd.1.man $pkgdir/usr/share/man/man1/murmurd.1
    install -Dm644 ${srcdir}/murmur.tmpfiles.d \
                    ${pkgdir}/usr/lib/tmpfiles.d/murmur.conf
    install -Dm644 ${srcdir}/murmur.service \
                    ${pkgdir}/usr/lib/systemd/system/murmur.service
}

md5sums=('97220934051d7598ffe327f4f8ee1370'
         'b99061be67c54bf0bb4a061fd1280281'
         'c56726cb267ead68f37f35c28e36e6fa'
         'e00d0d2610b77efebce5b3556609251f'
         '5ad8798aea4f8b70f47f16df64f755b3'
         '11c1cc875e8021739ec2087252cf0533'
         '1c976acaa7657a0b319e20c1a269e732')
