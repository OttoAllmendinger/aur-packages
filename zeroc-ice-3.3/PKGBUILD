# Maintainer: Otto Allmendinger <otto.allmendinger@gmail.com>

pkgname="zeroc-ice"
pkgver=3.3.1
pkgrel=9
pkgdesc="An object-oriented middleware that provides object-oriented Remote \
Procedure Call functionality"
arch=("i686" "x86_64")
url="http://www.zeroc.com"
license=("GPL" "custom: ICE license")
makedepends=("apache-ant")
depends=("db" "openssl" "expat" "mcpp" "python" "php" 
            "java-berkeleydb>=4.8.24-2"
            "jgoodies-forms>=1.2.1-2"
            "jgoodies-looks")

source=("http://www.zeroc.com/download/Ice/3.3/Ice-$pkgver.tar.gz"
        "ice.pth" "IceSSL-${pkgver}.patch")

md5sums=('1f37dfcec4662fcde030553fb447d064'
         '5e1e1befd5cbd5262ec47770f591bd14'
         '5db00cbd1cd7b6570064fb92fffab185')

build() {

    _builddir="${srcdir}/Ice-$pkgver-build"
    _datadir="${pkgdir}/usr/share/Ice-$pkgver"
    if [ $CARCH = "i686" ]; then
        _libdir=${_builddir}/lib
    else
        _libdir=${_builddir}/lib64
    fi

    rm -rf ${_builddir}
    mkdir ${_builddir}
    mkdir ${_libdir} -p
    mkdir ${_builddir}/bin

    mkdir -p ${_datadir}

    # IceSSL patch

    msg2 "Applying IceSSL patch..."

    patch -p1 "${srcdir}/Ice-${pkgver}/cpp/src/IceSSL/Instance.cpp" < \
        "IceSSL-${pkgver}.patch" || return 1


    # this needs to be done always

    cd ${startdir}/src/Ice-$pkgver/cpp/src
    make OPTIMIZE=yes embedded_runpath_prefix="" || return 1



    # C++

    echo "INSTALL C++"
    cd ${startdir}/src/Ice-$pkgver/cpp
    make prefix=${_builddir} embedded_runpath_prefix="" install


    install -dm755 ${pkgdir}/usr/bin
    mv ${_builddir}/bin/* ${pkgdir}/usr/bin/

    install -dm755 ${pkgdir}/usr/lib
    cp ${_libdir}/*.so ${pkgdir}/usr/lib/

    install -dm755 ${pkgdir}/usr/include
    mv ${_builddir}/include/* ${pkgdir}/usr/include/



    # Python

    cd ${startdir}/src/Ice-$pkgver/py
    make OPTIMIZE=yes embedded_runpath_prefix="" || return 1
    make prefix=${_builddir} embedded_runpath_prefix="" install
    install -dm755 "${pkgdir}/usr/lib/python2.6/site-packages/"
    mv ${_builddir}/python "${pkgdir}/usr/lib/python2.6/site-packages/Ice"
    cp -p ${startdir}/ice.pth "${pkgdir}/usr/lib/python2.6/site-packages/"



    # PHP

    cd ${startdir}/src/Ice-$pkgver/php

    # Patching source (thanks to Asaru)
    sed -i "s/ZVAL_ADDREF/Z_ADDREF_P/" src/IcePHP/Marshal.cpp || return 1

    make OPTIMIZE=yes embedded_runpath_prefix="" || return 1
    make prefix=${_builddir} embedded_runpath_prefix="" install
    install -Dm755 ${_libdir}/IcePHP.so ${pkgdir}/usr/lib/IcePHP.so



    # Java

    cd ${startdir}/src/Ice-$pkgver/java

    for jpkg in "jgoodies-looks" "jgoodies-forms" "berkeleydb" "proguard"; do
        for jar in /usr/share/java/$jpkg/*.jar; do
            CLASSPATH=$CLASSPATH:$jar
        done
    done

    export CLASSPATH

    ant jar || return 1

    ant -Dprefix=${_builddir} install

    mkdir -p "${pkgdir}/usr/share/java/zeroc-ice/"
    cp ${_libdir}/* "${pkgdir}/usr/share/java/zeroc-ice/"



    # Slice files

    msg2 "copying slice files..."
    cp -r ${_builddir}/slice ${_datadir}/slice


    msg2 "copying ImportKey..."
    cp ${_builddir}/lib/ImportKey.class ${_datadir}


    # License

    install -Dm644 ${srcdir}/Ice-$pkgver/ICE_LICENSE \
                ${pkgdir}/usr/share/licenses/zeroc-ice/ICE_LICENSE

}
