# $Id: PKGBUILD 69125 2010-02-17 20:18:24Z jgc $
# Maintainer: Jan de Groot <jgc@archlinux.org>

# Contributor: Otto Allmendinger <otto.allmendinger@gmail.com>

#   added patch for chromium bug:
#   http://code.google.com/p/chromium/issues/detail?id=36151
#
#   caused by gtk bug:
#   https://bugzilla.gnome.org/show_bug.cgi?id=608615#c5
#
#   patch source:
#   http://git.gnome.org/browse/gtk+/commit/?id=325cbef27edd58b801509a2016aee6ab6e49d2f1

pkgname=gtk2
pkgver=2.18.7
pkgrel=2
pkgdesc="The GTK+ Toolkit (v2)"
arch=(i686 x86_64)
url="http://www.gtk.org/"
install=gtk2.install
depends=('atk>=1.28.0' 'pango>=1.26.1' 'libxcursor' 'libxinerama' 'libxrandr>=1.3.0' 'libxi>=1.2.1' 'libxcomposite' 'libxdamage' 'heimdal>=1.3.1' 'gnutls>=2.8.3' 'shared-mime-info' 'cairo>=1.8.8-2' 'libtiff>=3.9.2-2' 'libcups>=1.4.2-3')
makedepends=('pkgconfig' 'gtk-doc')
replaces=('gtkprint-cups' 'gail')
conflicts=('gtkprint-cups' 'gail')
provides=('gail=1.22.3')
options=('!libtool' '!docs')
backup=(etc/gtk-2.0/gtkrc)
license=('LGPL')
source=(http://ftp.gnome.org/pub/gnome/sources/gtk+/2.18/gtk+-${pkgver}.tar.bz2
        xid-collision-debug.patch bug-609952.patch)
sha256sums=('602d4f8098e1620231788770e1d9b520d5824da3f01d86e3b4fb59f1d67e1464'
            'd758bb93e59df15a4ea7732cf984d1c3c19dff67c94b957575efea132b8fe558'
            '61ead70a439f873cc66676ef8a0b4076d2c0f44534c1e5d8898c42ef3a659b33')
build() {
  cd "${srcdir}/gtk+-${pkgver}"
  patch -Np1 -i "${srcdir}/xid-collision-debug.patch" || return 1
  patch -Np1 -i "${srcdir}/bug-609952.patch" || return 1

  CXX=/bin/false ./configure --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --with-xinput=yes \
      --without-libjasper \
      --with-included-loaders=png || return 1
  make || return 1
  make DESTDIR="${pkgdir}" install || return 1

  echo 'gtk-fallback-icon-theme = "gnome"' > "${pkgdir}/etc/gtk-2.0/gtkrc" || return 1
}
